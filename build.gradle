// we're using this plugin to combine and minify the JavaScript in this project
plugins {
    id "com.eriwen.gradle.js" version "2.14.1"
    id "com.eriwen.gradle.css" version "2.14.0"
}

// apply the JavaScript plugin
apply plugin: 'js'
// apply the css plugin
apply plugin: 'css'


def subProjects = ['toolbar', 'thumbnails', 'loading', 'drawingToolbars']

subProjects.each { name ->
    // create JavaScript minification tasks for each of the customisable parts of this branding pack
    task "minify${name.capitalize()}Js"(type: com.eriwen.gradle.js.tasks.MinifyJsTask) {
        group = 'JavaScript Minification'
        description "Minifies the JavaScript file for the ${name}"
        source = file("${name}/${name}.js")
        dest = file("${name}/${name}.min.js")
        sourceMap = file("${name}/${name}.min.sourcemap.json")
        closure {
            // set the log level
            warningLevel = 'QUIET'
        }
        ext {
            isMinifyTask = true
        }
    }
    // create LESS compilation tasks for each of the customisable parts of this branding pack
    task "compile${name.capitalize()}Css"(type: com.eriwen.gradle.css.tasks.LessTask) {
        group = 'LESS Compilation'
        description "Compiles the LESS file for ${name}"
        source = file("${name}/style.less")
        dest = file("${name}/")
        ext {
            isLessTask = true
        }
    }
}

// minifies all the JavaScript in this project by depending on the dynamic minification tasks created above
task minifyAllJs {
    group = 'JavaScript Minification'
    description = 'Minifies all the JavaScript files in this project'
    // keep a list of tasks that we should depend on
    def toDependOn = []
    tasks.each { aTask ->
        // if it's a minification task, add it to the toDependOn list
        if (aTask.ext.has("isMinifyTask") && aTask.ext.isMinifyTask) {
            toDependOn.add(aTask)
        }
    }
    // depend on all the minification tasks found
    dependsOn = toDependOn
}

// compiles all the LESS in this project by depending on the dynamic LESS tasks created above
task compileAllCss {
    group = 'LESS Compilation'
    description = 'Compiles all the LESS files in this project'
    // keep a list of tasks that we should depend on
    def toDependOn = []
    tasks.each { aTask ->
        // if it's a LESS task, add it to the toDependOn list
        if (aTask.ext.has("isLessTask") && aTask.ext.isLessTask) {
            toDependOn.add(aTask)
        }
    }
    // depend on all the LESS tasks found
    dependsOn = toDependOn
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}
