// we're using this plugin to combine and minify the JavaScript in this project
plugins {
    id "com.eriwen.gradle.js" version "2.14.1"
}

// apply the JavaScript plugin
apply plugin: 'js'


// create JavaScript minification tasks for each of the customisable parts of this branding pack
['toolbar', 'thumbnails', 'loading', 'drawingToolbars'].each { name ->
    task "minify${name.capitalize()}Js"(type: com.eriwen.gradle.js.tasks.MinifyJsTask) {
        group = 'JavaScript Minification'
        description "Minifies the JavaScript file for the ${name}"
        source = file("${name}/${name}.js")
        dest = file("${name}/${name}.min.js")
        sourceMap = file("${name}/${name}.min.sourcemap.json")
        closure {
            // set the log level
            warningLevel = 'QUIET'
        }
        ext {
            isMinifyTask = true
        }
    }
}

// minifies all the JavaScript in this project by depending on the dynamic minification tasks created above
task minifyAllJs {
    group = 'JavaScript Minification'
    description = 'Minifies all the JavaScript files in this project'
    // keep a list of tasks that we should depend on
    def toDependOn = []
    tasks.each { aTask ->
        // if it's a minification task, add it to the toDependOn list
        if (aTask.ext.has("isMinifyTask") && aTask.ext.isMinifyTask) {
            toDependOn.add(aTask)
        }
    }
    // depend on all the minification tasks found
    dependsOn = toDependOn
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}
